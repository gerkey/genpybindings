#
# Introduce a scope so's the libraries don't escape.
# On lucid (cmake 2.8.0) the boost_python escapes to other
# link lines and causes errors.
#
if(GENPYBINDINGS_EXTRAS_INCLUDED)
  return()
endif()
set(GENPYBINDINGS_EXTRAS_INCLUDED TRUE)

function(find_genpybinding_deps)
  find_package(Boost COMPONENTS python)
  set(BOOST_PYTHON_LIBRARY ${Boost_LIBRARIES} PARENT_SCOPE)
  find_package(PythonLibs)
endfunction()
find_genpybinding_deps()

if(@PACKAGE_NAME@_SOURCE_DIR)
  find_program_required(GENPYBINDINGS_BIN gen_pybindings.py
    PATHS ${@PACKAGE_NAME@_SOURCE_DIR}/scripts)
  set(GENPYBINDINGS_TEMPLATE_DIR ${@PACKAGE_NAME@_SOURCE_DIR}/scripts)
else()
  find_program_required(GENPYBINDINGS_BIN gen_pybindings.py
    PATHS @CMAKE_INSTALL_PREFIX@/share/genpybindings/bin
    )
  set(GENPYBINDINGS_TEMPLATE_DIR ${CMAKE_INSTALL_PREFIX}/share/genpybindings)
endif()

# Generate .cpp
# The generated .h files should be added ALL_GEN_OUTPUT_FILES_cpp
macro(_generate_msg_pybindings ARG_PKG ARG_MSG ARG_IFLAGS ARG_MSG_DEPS ARG_GEN_OUTPUT_DIR)

  #Create input and output filenames
  get_filename_component(MSG_NAME ${ARG_MSG} NAME)
  get_filename_component(MSG_SHORT_NAME ${ARG_MSG} NAME_WE)

  set(MSG_GENERATED_NAME ${MSG_SHORT_NAME}.cpp)
  set(GEN_OUTPUT_FILE ${ARG_GEN_OUTPUT_DIR}/${MSG_GENERATED_NAME})

  assert(CATKIN_ENV)
  add_custom_command(OUTPUT ${GEN_OUTPUT_FILE}
    DEPENDS ${GENPYBINDINGS_BIN} ${ARG_MSG} ${ARG_MSG_DEPS}
    ${GENPYBINDINGS_TEMPLATE_DIR}/msg.cpp.template
    ${GENPYBINDINGS_TEMPLATE_DIR}/srv.cpp.template
    COMMAND ${CATKIN_ENV} ${PYTHON_EXECUTABLE} ${GENPYBINDINGS_BIN} ${ARG_MSG}
    ${ARG_IFLAGS}
    -p ${ARG_PKG}
    -o ${ARG_GEN_OUTPUT_DIR}
    -e ${GENPYBINDINGS_TEMPLATE_DIR}
    COMMENT "Generating boost::python code from ${ARG_PKG}/${MSG_NAME}"
    )

   list(APPEND ALL_GEN_OUTPUT_FILES_pybindings ${GEN_OUTPUT_FILE})
endmacro()

# Generate .cpp
# The generated .h files should be added ALL_GEN_OUTPUT_FILES_cpp
macro(_generate_srv_pybindings ARG_PKG ARG_MSG ARG_IFLAGS ARG_MSG_DEPS ARG_GEN_OUTPUT_DIR)

  #Create input and output filenames
  get_filename_component(MSG_NAME ${ARG_MSG} NAME)
  get_filename_component(MSG_SHORT_NAME ${ARG_MSG} NAME_WE)

  set(MSG_GENERATED_NAME ${MSG_SHORT_NAME}.cpp)
  set(GEN_OUTPUT_FILES
    ${ARG_GEN_OUTPUT_DIR}/${MSG_SHORT_NAME}.cpp
    ${ARG_GEN_OUTPUT_DIR}/${MSG_SHORT_NAME}Request.cpp
    ${ARG_GEN_OUTPUT_DIR}/${MSG_SHORT_NAME}Response.cpp
    )

  assert(CATKIN_ENV)
  add_custom_command(OUTPUT ${GEN_OUTPUT_FILES}
    DEPENDS ${GENPYBINDINGS_BIN} ${ARG_MSG} ${ARG_MSG_DEPS}
    ${GENPYBINDINGS_TEMPLATE_DIR}/msg.cpp.template
    ${GENPYBINDINGS_TEMPLATE_DIR}/srv.cpp.template
    COMMAND ${CATKIN_ENV} ${PYTHON_EXECUTABLE} ${GENPYBINDINGS_BIN} ${ARG_MSG}
    ${ARG_IFLAGS}
    -p ${ARG_PKG}
    -o ${ARG_GEN_OUTPUT_DIR}
    -e ${GENPYBINDINGS_TEMPLATE_DIR}
    COMMENT "Generating boost::python code from ${ARG_PKG}/${MSG_NAME}"
    )

   list(APPEND ALL_GEN_OUTPUT_FILES_pybindings ${GEN_OUTPUT_FILES})
endmacro()

macro(_generate_module_pybindings ARG_PKG ARG_GEN_OUTPUT_DIR ARG_GENERATED_FILES)

  # create a module.cpp file here
  # calling all the export_MSG functions
  # BOOST_PYTHON_MODULE(std_msgs)
  # {
  #   export_MSG1();
  #   export_MSG2();
  # }

  find_package(ROS REQUIRED COMPONENTS
    cpp_common roscpp_serialization rostime roscpp_traits)

  include_directories(${ROS_INCLUDE_DIRS})

  set(GEN_OUTPUT_FILE ${ARG_GEN_OUTPUT_DIR}/module.cpp)

  assert(CATKIN_ENV)
  add_custom_command(OUTPUT ${GEN_OUTPUT_FILE}
    DEPENDS ${GENPYBINDINGS_BIN} ${ARG_GENERATED_FILES} ${ARG_MSG_DEPS}
    ${GENPYBINDINGS_TEMPLATE_DIR}/module.cpp.template
    COMMAND ${CATKIN_ENV} ${PYTHON_EXECUTABLE} ${GENPYBINDINGS_BIN}
    -m
    -p ${ARG_PKG}
    -o ${ARG_GEN_OUTPUT_DIR}
    -e ${GENPYBINDINGS_TEMPLATE_DIR}
    COMMENT "Generating boost::python module for ${ARG_PKG}"
    )

  list(APPEND ALL_GEN_OUTPUT_FILES_pybindings ${GEN_OUTPUT_FILE})
  # rule to put __init__.py into build dir
  file(MAKE_DIRECTORY lib/${ARG_PKG})
  file(COPY ${GENPYBINDINGS_TEMPLATE_DIR}/__init__.py
    DESTINATION lib/${ARG_PKG}
    )
  install(FILES ${GENPYBINDINGS_TEMPLATE_DIR}/__init__.py
    DESTINATION lib/${ARG_PKG}
    )
  # build module
  set(boost_lib_target "${ARG_PKG}_pybindings_module")

  #
  # Well this is kinda awful, but works.  Manufacture a subdirectory
  # to ensure that the include_directories is scoped and doesn't
  # infect other targets.
  #
  file(MAKE_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/pybindings-src)
  file(WRITE ${CMAKE_CURRENT_BINARY_DIR}/pybindings-src/CMakeLists.txt "

    set_source_files_properties(${ALL_GEN_OUTPUT_FILES_pybindings}
      PROPERTIES
      GENERATED TRUE
      )

    include_directories(
      ${Boost_INCLUDE_DIRS} ${PYTHON_INCLUDE_PATH} ${CMAKE_BINARY_DIR}/gen/cpp)

    add_library(${boost_lib_target} SHARED ${ARG_GENERATED_FILES} ${GEN_OUTPUT_FILE})
    set_target_properties(${boost_lib_target} PROPERTIES
      PREFIX \"\"
      OUTPUT_NAME msg
      LIBRARY_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib/${ARG_PKG}
      )

    add_dependencies(${boost_lib_target} ${ARG_PKG}_gencpp)
    target_link_libraries(${boost_lib_target}
      ${BOOST_PYTHON_LIBRARY} ${ROS_LIBRARIES}
      )

    install(TARGETS ${boost_lib_target} LIBRARY DESTINATION lib/${ARG_PKG})
")

  add_subdirectory(${CMAKE_CURRENT_BINARY_DIR}/pybindings-src
    ${CMAKE_CURRENT_BINARY_DIR}/pybindings
    )

endmacro()

set(genpybindings_INSTALL_DIR lib/pybindings)